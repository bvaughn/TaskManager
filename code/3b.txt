package {
	import com.rosettastone.library.taskmanager.CompositeTask;
	import com.rosettastone.library.taskmanager.PlaySoundTask;
	import com.rosettastone.library.taskmanager.URLRequestTask;
	
	import flash.events.MouseEvent;
	import flash.media.Sound;
	import flash.net.URLRequest;
	
	import spark.components.Button;
	
	public class SampleController_3_WithTaskManager {
		
		private var _button:Button;
		private var _compositeTask:CompositeTask;
		
		public function SampleController_3_WithTaskManager( button:Button ) {
			_button = button;
		}
		
		public function start():void {
			_button.addEventListener( MouseEvent.CLICK, onButtonClick );
			
			if ( _compositeTask && !_compositeTask.running ) {
				_compositeTask.run();
			}
		}
		
		public function stop():void {
			_button.removeEventListener( MouseEvent.CLICK, onButtonClick );
			
			if ( _compositeTask && _compositeTask.running ) {
				_compositeTask.interrupt();
			}
		}
		
		private function onButtonClick( event:MouseEvent ):void {
			_button.enabled = false;
			
			_compositeTask = new CompositeTask( null, false );
			_compositeTask.addTask(
				new URLRequestTask(
					new URLRequest( "fake_url_for_sound_start" ) ) );
			_compositeTask.addTask(
				new PlaySoundTask(
					new Sound(
						new URLRequest( "fake_sound_url" ) ) ) );
			_compositeTask.addTask(
				new URLRequestTask(
					new URLRequest( "fake_url_for_sound_finished" ) ) );
			_compositeTask.withCompleteHandler(
				function():void {
					_button.enabled = true;
				} );
			_compositeTask.withErrorHandler(
				function():void {
					// Handle error
				} );
			_compositeTask.run();
		}
	}
}